initialize() {
	// Setting the seed for replicability
	setSeed(RUN);
	
	// Set the wd
	setwd("/path/to/wd");
	
	// Start a nucleotide-explicit model
	initializeSLiMOptions(nucleotideBased=T);
	
	// Define the overall mutation rate as follows (rates from Stammnitz et al. 2023):
	// - DFT1 rate, per year = 215.5 (BEAST median; Table 2)
	// - DFT1 rate, per year per nucleotide = 215.5 / 2,983,750,195 = 7.222e-8
	// - Final = 7.222e-8 / 3 (accounts for per-nucleotide transition/transversion rate)
	defineConstant("R", mmJukesCantor(7.222e-8 / 3));
	
	// Read in CDS fasta as ancestral sequence
	defineConstant("L", initializeAncestralNucleotides("devil_protein_coding_CDS_SLiM_ref.fa"));
	
	// Initialize mutation types (should be only neutral mutations for the neutral model)
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);
	
	// Set up genome
	initializeGenomicElementType("g1", m1, 1.0, R);
	initializeGenomicElement(g1, 0, L-1);
	
	//No recombination
	initializeRecombinationRate(0);
}

1 early() {
	sim.addSubpop("p1", 25);
	p1.setCloningRate(1);
}

// Changing Ne based on Patton et al. 2020.
12:32 early() {
	defineGlobal("i", community.tick-12);
	Ne_changes = c(26, 27, 29, 31, 36, 44, 59, 88, 143, 201, 251, 311, 472, 999, 1523, 1821, 1977, 2036, 2048, 2050, 2051);
	p1.setSubpopulationSize(Ne_changes[i]);
}

// Sampling to follow the 118 WGS samples.
// The samples are chosen randomly and a separate VCF is created for each (must be merged after the model finishes)
23:38 late(){
	// Get appropriate sample n for the tick (also, if this is the first sample overwrite the VCF)
	defineGlobal("n", community.tick-23);
	sampleN = c(5, 6, 2, 9, 4, 8, 6, 2, 17, 13, 6, 6, 15, 12, 3, 4);
	outputName = paste0("VCF_output/run", RUN, "/parts/tick", community.tick, ".vcf");
	
	
	// Sample individuals and write to separate VCFs
	allIndividuals = p1.individuals;
	sampledIndividuals = sample(allIndividuals, sampleN[n]);
	sampledIndividuals.haplosomes.outputHaplosomesToVCF(outputName);
}
